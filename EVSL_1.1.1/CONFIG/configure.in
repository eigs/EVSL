dnl *********************************************************************
dnl * File Name: configure.in
dnl *
dnl * This file is read when autoconf is run and the configure script is
dnl * generated. 
dnl * Configure.in is structured as follows:
dnl * initialization information
dnl * determine the current architecture
dnl * set user specified compilers and any other user specified options
dnl *
dnl * For the given the architecture, an optimal compiler is
dnl * found (if not specified by the user). If the architecture is
dnl * unknown or none of the preferred compilers are available then
dnl * default compilers are found.  
dnl *
dnl * For each chosen compiler, the appropriate flags are set for 
dnl * optimization, debugging, MPI and pthreading (as required) and the
dnl * C preprocessor is checked.
dnl * 
dnl * Finally, library flags are added and AC_SUBST is used to export
dnl * all necessary macro values.  
dnl *********************************************************************

dnl *********************************************************************
dnl * Initialization Information
dnl * Set package information so it only has to be modified in one place
dnl *********************************************************************

m4_define([M4_EVSL_NAME],    [EVSL])
m4_define([M4_EVSL_VERSION], [1.1.1])
m4_define([M4_EVSL_DATE],    [2018/03/23])
m4_define([M4_EVSL_TIME],    [00:00:00])
m4_define([M4_EVSL_BUGS],    [saad@cs.umn.edu])
m4_define([M4_EVSL_SRCDIR],  [`pwd`])

AC_PREREQ(2.59)
AC_REVISION($Id$)
AC_INIT(M4_EVSL_NAME, M4_EVSL_VERSION)
AC_CONFIG_HEADERS([INC/EVSL_config.h:CONFIG/EVSL_config.h.in])
AC_COPYRIGHT([EVSL is free software; you can redistribute it and/or modify it under the
terms of the GNU Lesser General Public License (as published by the Free
Software Foundation) version 2.1 dated February 1999.])
AC_CONFIG_AUX_DIR(CONFIG)
AC_CONFIG_SRCDIR([../"M4_EVSL_NAME"_"M4_EVSL_VERSION"])
dnl * Change default prefix from /usr/local to ./EVSL
dnl * Note that $ expansion of shell variables doesn't work with this AC macro.
AC_PREFIX_DEFAULT(["M4_EVSL_SRCDIR"/EVSL])

dnl *********************************************************************
dnl * Initialize some variables
dnl *
dnl * For some reason, this type of variable initialization doesn't work
dnl * if done before AC_INIT above.  To keep the release info at the top
dnl * of the file, 'm4_define' is used to define m4 macros first.
dnl *********************************************************************
EVSL_NAME="M4_EVSL_NAME"
EVSL_VERSION="M4_EVSL_VERSION"
EVSL_DATE="M4_EVSL_DATE"
EVSL_TIME="M4_EVSL_TIME"
EVSL_BUGS="M4_EVSL_BUGS"
EVSL_SRCDIR="M4_EVSL_SRCDIR"

AC_DEFINE_UNQUOTED(EVSL_RELEASE_NAME,    ["$EVSL_NAME"],    [Release name])
AC_DEFINE_UNQUOTED(EVSL_RELEASE_VERSION, ["$EVSL_VERSION"], [Version number])
AC_DEFINE_UNQUOTED(EVSL_RELEASE_DATE,    ["$EVSL_DATE"],    [Date of release])
AC_DEFINE_UNQUOTED(EVSL_RELEASE_TIME,    ["$EVSL_TIME"],    [Time of release])
AC_DEFINE_UNQUOTED(EVSL_RELEASE_BUGS,    ["$EVSL_BUGS"],    [Bug reports])

AC_SUBST(EVSL_NAME)
AC_SUBST(EVSL_VERSION)
AC_SUBST(EVSL_DATE)
AC_SUBST(EVSL_TIME)
AC_SUBST(EVSL_BUGS)

AC_SUBST(EVSL_SRCDIR)

dnl *********************************************************************
dnl * Clear variables defined by AC_INIT to avoid name conflicts with
dnl * other packages.
dnl *********************************************************************
PACKAGE_DATE=
PACKAGE_TIME=
PACKAGE_DATETIME=
PACKAGE_NAME=
PACKAGE_VERSION=
PACKAGE_STRING=
PACKAGE_TARNAME=
PACKAGE_BUGREPORT=

dnl *********************************************************************
dnl * Initialize evsl variables
dnl *********************************************************************
evsl_using_Ccompiler=yes
evsl_using_CXXcompiler=no
evsl_using_openmp=no
evsl_with_fortran=yes
evsl_with_debug=no
evsl_using_evsl_cxsparse=yes
evsl_using_suitesparse=no
evsl_using_mkl_pardiso=no
evsl_with_metis=no
evsl_using_int=yes
evsl_using_double=yes
evsl_using_evsl_blas=yes
evsl_using_evsl_lapack=yes
evsl_using_intel_mkl=no
evsl_using_intel_mkl_static=yes
evsl_cholmod_use_long=yes
evsl_using_shared_lib=no
evsl_using_mac_frame_accelerate=no
evsl_using_cuda=no
dnl *********************************************************************
dnl * Determine BUILD, HOST, and TARGET types
dnl *********************************************************************
if test "x$build_alias" = "x"
then
   AC_CANONICAL_BUILD
fi

if test "x$host_alias" = "x"
then
   AC_CANONICAL_HOST
fi

if test "x$target_alias" = "x"
then
   target_alias=$host_alias
fi

dnl *********************************************************************
dnl * Define optional features
dnl *********************************************************************
AC_ARG_ENABLE(debug,
AS_HELP_STRING([--enable-debug],
               [Set compiler flags for debugging (default is NO)]),
[case "${enableval}" in
    yes) evsl_with_debug=yes;;
    no)  evsl_with_debug=no;;
    *)   AC_MSG_ERROR([Bad value ${enableval} for --enable-debug]) ;;
 esac],
[evsl_with_debug=no]
)

AC_ARG_ENABLE(shared,
AS_HELP_STRING([--enable-shared],
               [Build shared libraries (default is NO).]),
[case "${enableval}" in
    yes) evsl_using_shared_lib=yes;;
    no)  evsl_using_shared_lib=no;;
    *)   AC_MSG_ERROR([Bad value ${enableval} for --enable-shared]) ;;
 esac],
[evsl_using_shared_lib=no]
)

AC_ARG_WITH(cxxcompiler,
AS_HELP_STRING([--with-cxxcompiler],
               [Use C++ compiler for compiling (default is NO)]),
[case "${withval}" in
    yes) evsl_using_CXXcompiler=yes;;
    no)  evsl_using_CXXcompiler=no;;
    *)   AC_MSG_ERROR([Bad value ${withval} for --with-cxxcompiler]) ;;
 esac],
[evsl_using_CXXcompiler=no]
)

AC_ARG_WITH(openmp,
AS_HELP_STRING([--with-openmp],
               [Use OpenMP (default is NO). This may affect which compiler is chosen.]),
[case "${withval}" in
    yes) evsl_using_openmp=yes;;
    no)  evsl_using_openmp=no ;;
    *)   AC_MSG_ERROR([Bad value ${withval} for --with-openmp]) ;;
 esac],
[evsl_using_openmp=no]
)

AC_ARG_WITH(blas-lib,
AS_HELP_STRING([--with-blas-lib=LIB],
               [LIB is space-separated list (enclosed in quotes) of BLAS libraries
                (full path and name, e.g., /usr/local/lib/libblas.a]),
[for blas_lib in $withval; do
    LIBBLAS+="$blas_lib "
 done;
 evsl_using_evsl_blas=no]
)

AC_ARG_WITH(blas-lib-name,
AS_HELP_STRING([--with-blas-lib-name=LIB],
               [LIB is space-separated list (enclosed in quotes) of BLAS libraries
                (base name only, e.g., blas, and set path by --with-blas-lib-dir=DIR]),
[for blas_lib in $withval; do
    LIBBLAS+="-l$blas_lib "
 done;
 evsl_using_evsl_blas=no]
)

AC_ARG_WITH(blas-lib-dir,
AS_HELP_STRING([--with-blas-lib-dir=DIR],
               [DIR is space-separated list (enclosed in quotes) of directories containing BLAS libraries that are
                set by --with-blas-lib-name, e.g., /usr/local/lib]),
[for blas_dir in $withval; do
    LIBBLAS_DIR+="-L$blas_dir "
 done;
 evsl_using_evsl_blas=no]
)

AC_ARG_WITH(lapack-lib,
AS_HELP_STRING([--with-lapack-lib=LIB],
               [LIB is space-separated list (enclosed in quotes) of LAPACK libraries
                (full path and name, e.g., /usr/local/lib/liblapack.a]),
[for lapack_lib in $withval; do
    LIBLAPACK+="$lapack_lib "
 done;
 evsl_using_evsl_lapack=no]
)

AC_ARG_WITH(lapack-lib-name,
AS_HELP_STRING([--with-lapack-lib-name=LIB],
               [LIB is space-separated list (enclosed in quotes) of LAPACK libraries
                (base name only, e.g., lapack, and set path by --with-lapack-lib-dir=DIR]),
[for lapack_lib in $withval; do
    LIBLAPACK+="-l$lapack_lib "
 done;
 evsl_using_evsl_lapack=no]
)

AC_ARG_WITH(lapack-lib-dir,
AS_HELP_STRING([--with-lapack-lib-dir=DIR],
               [DIR is space-separated list (enclosed in quotes) of directories containing LAPACK libraries that are
                set by --with-lapack-lib-name, e.g., /usr/local/lib]),
[for lapack_dir in $withval; do
    LIBLAPACK_DIR+="-L$lapack_dir "
 done;
 evsl_using_evsl_lapack=no]
)

AC_ARG_WITH(intel-mkl,
AS_HELP_STRING([--with-intel-mkl],
               [Use Intel MKL for BLAS, LAPACK and SpMV subroutines (default is NO).]),
[case "${withval}" in
    yes) evsl_using_intel_mkl=yes
         evsl_using_evsl_blas=no
         evsl_using_evsl_lapack=no;;
    no)  evsl_using_intel_mkl=no;;
    *)   AC_MSG_ERROR([Bad value ${withval} for --with-intel-mkl]) ;;
 esac],
[evsl_using_intel_mkl=no]
)

AC_ARG_WITH(intel-mkl-static,
AS_HELP_STRING([--with-intel-mkl-static],
               [Use static linking with Intel MKL (default is YES).]),
[case "${withval}" in
    yes) evsl_using_intel_mkl_static=yes;;
    no)  evsl_using_intel_mkl_static=no;;
    *)   AC_MSG_ERROR([Bad value ${withval} for --with-intel-mkl-static]) ;;
 esac],
[evsl_using_intel_mkl_static=no]
)

AC_ARG_WITH(framework-accelerate,
AS_HELP_STRING([--with-framework-accelerate],
               [Use Apple Accelerate framework (default is NO, for Mac only)]),
[case "${withval}" in
    yes) evsl_using_mac_frame_accelerate=yes
         evsl_using_evsl_blas=no
         evsl_using_evsl_lapack=no;;
    no)  evsl_using_mac_frame_accelerate=no;;
    *)   AC_MSG_ERROR([Bad value ${withval} for --with-framework-accelerate]) ;;
 esac],
[evsl_using_mac_frame_accelerate=no]
)

AC_ARG_WITH(suitesparse-dir,
AS_HELP_STRING([--with-suitesparse-dir=DIR],
               [Use UMFPACK and CHOLMOD in Suitesparse as direct solvers. DIR is the root directory of SUITESPARSE (http://faculty.cse.tamu.edu/davis/suitesparse.html), e.g., /usr/local/SuiteSparse]),
[for suitesparse_dir in $withval; do
    SUITESPARSE_DIR+="$suitesparse_dir"
 done;
 evsl_using_evsl_cxsparse=no
 evsl_using_suitesparse=yes]
)

AC_ARG_WITH(mkl-pardiso,
AS_HELP_STRING([--with-mkl-pardiso],
               [Use Pardiso in MKL as direct solver (default is NO). This option should be used together with --with-intel-mkl]),
[case "${withval}" in
    yes) evsl_using_mkl_pardiso=yes
         evsl_using_evsl_cxsparse=no;;
    no)  evsl_using_mkl_pardiso=no;;
    *)   AC_MSG_ERROR([Bad value ${withval} for --with-mkl-pardiso]) ;;
 esac],
[evsl_using_mkl_pardiso=no]
)

AC_ARG_WITH(metis-dir,
AS_HELP_STRING([--with-metis-dir=DIR],
               [DIR is the build directory of metis (5.1.0) (http://glaros.dtc.umn.edu/gkhome/metis/metis/overview), e.g., /usr/local/metis-5.1.0/build/Linux-x86_64]),
[for metis_dir in $withval; do
    METIS_DIR+="$metis_dir"
 done;
 evsl_with_metis=yes]
)

AC_ARG_WITH(extra-lib-name,
AS_HELP_STRING([--with-extra-lib-name=LIB],
               [LIB is space-separated list (enclosed in quotes) of extra libraries
                (base name only, e.g., 'pthread gfortran']),
[for extra_lib in $withval; do
    EXTRA_LIBS+="-l$extra_lib "
 done]
)

AC_ARG_WITH(extra-lib-dir,
AS_HELP_STRING([--with-extra-lib-dir=DIR],
               [DIR is space-separated list (enclosed in quotes) of directories containing extra libraries that are
                set by --with-extra-lib-name, e.g., /opt/lib]),
[for extra_dir in $withval; do
    LIBS_DIR+="-L$extra_dir "
 done]
)

AC_ARG_WITH(cuda,
AS_HELP_STRING([--with-cuda],
               [Use CUDA. Require cuda-5.0 or higher (default is NO).]),
[case "$withval" in
    yes) evsl_using_cuda=yes;;
    no)  evsl_using_cuda=no ;;
    *)   evsl_using_cuda=no ;;
 esac],
[evsl_using_cuda=no]
)

dnl *********************************************************************
dnl * Set default AR value if not defined by the user
dnl *********************************************************************
if test "x$AR" = "x"
then
   AR="ar rc"
fi

dnl *********************************************************************
dnl * Check for general programs
dnl *********************************************************************
AC_PROG_MAKE_SET
AC_PROG_RANLIB

dnl *********************************************************************
dnl * Set default CC if not defined by the user
dnl *********************************************************************
if test "x$evsl_using_Ccompiler" = "xyes" -a "x$CC" = "x"
then
    AC_CHECK_PROGS(CC, [gcc cc clang xlc])
fi

dnl *********************************************************************
dnl * Set default CFLAGS if not defined by the user
dnl *********************************************************************
if test "x$evsl_using_Ccompiler" = "xyes" -a "x$CFLAGS" = "x"
then
   if test "x$evsl_with_debug" = "xyes"
   then
      CFLAGS="-g -O0 -Wall "
   else
      CFLAGS="-O2 -Wall "
   fi
fi

dnl *********************************************************************
dnl * Set default FC if not defined by the user
dnl *********************************************************************
if test "x$evsl_with_fortran" = "xyes" -a "x$FC" = "x"
then
    AC_CHECK_PROGS(FC, [gfortran f77 xlf])
fi

dnl *********************************************************************
dnl * Set default FCFLAGS if not defined by the user
dnl *********************************************************************
if test "x$evsl_with_fortran" = "xyes" -a "x$FCFLAGS" = "x"
then
   if test "x$evsl_with_debug" = "xyes"
   then
      CFLAGS="-g -O0 "
   else
      FCFLAGS="-O2 "
   fi
fi

dnl *********************************************************************
dnl * Set default CXX if not defined by the user
dnl *********************************************************************
if test "x$evsl_using_CXXcompiler" = "xyes" -a "x$CXX" = "x"
then
    AC_CHECK_PROGS(CXX, [g++ c++ clang++ xlC])
fi

dnl *********************************************************************
dnl * Check for compiler related programs 
dnl * Most of these AC_PROG_ macros check to see if the compilers works.
dnl *********************************************************************
if test "x$evsl_using_Ccompiler" = "xyes"
then
   AC_PROG_CC
fi

if test "x$evsl_with_fortran" = "xyes"
then
   AC_PROG_FC
   AC_FC_WRAPPERS
fi

if test "x$evsl_using_CXXcompiler" = "xyes"
then
   AC_PROG_CXX

   dnl use C++ compiler to compile C code
   CC=${CXX}
   dnl use '-x' to specify c++ language
   dnl if test "x$CC" = "xgcc" || test "x$CC" = "xicc"
   dnl then
   dnl CFLAGS+="-x c++ "
   dnl fi
fi

dnl *********************************************************************
dnl * add some default CFLAGS for gcc/g++/gfortran
dnl *********************************************************************
if test "x$CC" = "xgcc" || test "x$CC" = "xg++"
then
   CFLAGS+="-Wno-unused-result "
fi

if test "x$FC" = "xgfortran"
then
   FFLAGS+="-Wall -Wno-unused-variable -Wno-unused-dummy-argument "
fi

dnl *********************************************************************
dnl * set default link
dnl *********************************************************************
LINK_CC=${CC}
LINK_FC=${FC}

dnl *********************************************************************
dnl * set default libs
dnl *********************************************************************
LIBS+=${EXTRA_LIBS}

if test "x$LIBS" = "x"
then
AC_CHECK_LIB(m, sin, LIBS="-lm ")
else
AC_CHECK_LIB(m, sin, LIBS="$LIBS -lm ")
fi

dnl *********************************************************************
dnl * Set installation directories
dnl *********************************************************************
dnl EVSL_INSTALLDIR="${prefix}"
dnl EVSL_LIBINSTALL="${libdir}"
dnl EVSL_INCINSTALL="${includedir}"

dnl *********************************************************************
dnl * Define machine name and architecture
dnl *********************************************************************
AC_MSG_CHECKING(the hostname)
evsl_hostname=hostname
HOSTNAME="`$evsl_hostname`"
dnl * if $HOSTNAME is still empty, give it the value "unknown".
if test -z "$HOSTNAME"
then
   HOSTNAME=unknown
   AC_MSG_WARN(hostname is unknown)
else
   AC_MSG_RESULT($HOSTNAME)
fi

AC_MSG_CHECKING(the architecture)
HOSTARCH=`uname -s`
dnl * if $HOSTARCH is still empty, give it the value "unknown".
if test -z "$HOSTARCH"
then
   HOSTARCH=unknown
   AC_MSG_WARN(architecture is unknown)
else
   AC_MSG_RESULT($HOSTARCH)
fi

if test "x$HOSTARCH" = "xLinux"
then
   AC_DEFINE(EVSL_ARCH_LINUX, 1, [Linux platforms])
   LIBS+="-lrt "
fi

if test "x$HOSTARCH" = "xDarwin"
then
   AC_DEFINE(EVSL_ARCH_MAC, 1, [Mac platforms])
else
   evsl_using_mac_frame_accelerate=no
fi

dnl *********************************************************************
dnl * OpenMP
dnl *********************************************************************
if test "x$evsl_using_openmp" = "xyes"
then
   AC_DEFINE(EVSL_USING_OPENMP, 1, [Using OpenMP in EVSL])
   CFLAGS="$CFLAGS -fopenmp "
   if test "$evsl_using_cuda" = "yes"
   then
      LIBS="$LIBS -Xcompiler \"-fopenmp\" "
   else
      LIBS="$LIBS -fopenmp "
   fi
fi

dnl *********************************************************************
dnl * BLAS & LAPACK related information
dnl *********************************************************************
if test "x$evsl_using_mac_frame_accelerate" = "xyes"
then
   LIBS+="-framework Accelerate "
fi

dnl *********************************************************************
dnl * MKL
dnl *********************************************************************
AS_IF([test "x$evsl_using_intel_mkl" = "xyes" || test "x$evsl_using_mkl_pardiso" = "xyes"],
      [AC_CHECK_HEADERS(["${MKLROOT}/include/mkl.h"], [evsl_found_mkl=yes],[evsl_found_mkl=no])],
      [])

if test "x$evsl_using_intel_mkl" = "xyes" || test "x$evsl_using_mkl_pardiso" = "xyes"
then
   AC_SUBST(MKLROOT)
   AC_DEFINE(EVSL_USING_INTEL_MKL, 1, [Using Intel MKL for BLAS, LAPACK and SpMV])
   if test "x$evsl_found_mkl" != "xyes"
   then
      AC_MSG_WARN([************************************************])
      AC_MSG_WARN([unable to find mkl.h. Ensure that MKLROOT is set])
      AC_MSG_WARN([************************************************])
   fi

   dnl https://software.intel.com/en-us/articles/intel-mkl-link-line-advisor

   if test "x$HOSTARCH" = "xLinux"
   then
      if test "x$CC" = "xgcc" || test "x$CC" = "xg++"
      then
         CFLAGS+="-m64 "
      fi
      if test "x$FC" = "xgfortran"
      then
         FCFLAGS+="-m64 "
      fi
      dnl LIBS
      if test "x$evsl_using_openmp" = "xyes"
      then
         dnl openmp threading
         if test "x$evsl_using_intel_mkl_static" = "xyes"
         then
            dnl static link
            if test "x$LINK_CC" = "xicc"
            then
               LIBS+="-Wl,--start-group \${MKLROOT}/lib/intel64/libmkl_intel_lp64.a \${MKLROOT}/lib/intel64/libmkl_intel_thread.a \${MKLROOT}/lib/intel64/libmkl_core.a -Wl,--end-group -liomp5 -lpthread -lm -ldl "
            else
               LIBS+="-Wl,--start-group \${MKLROOT}/lib/intel64/libmkl_gf_lp64.a \${MKLROOT}/lib/intel64/libmkl_gnu_thread.a \${MKLROOT}/lib/intel64/libmkl_core.a -Wl,--end-group -lgomp -lpthread -lm -ldl "
            fi
         else
            dnl dynamic link
            if test "x$LINK_CC" = "xicc"
            then
               LIBS+="-L\${MKLROOT}/lib/intel64 -lmkl_intel_lp64 -lmkl_intel_thread -lmkl_core -liomp5 -lpthread -lm -ldl "
            else
               if test "$evsl_using_cuda" = "yes"
               then
                  LIBS+="-L\${MKLROOT}/lib/intel64 -Xlinker \"--no-as-needed\" -lmkl_gf_lp64 -lmkl_gnu_thread -lmkl_core -lgomp -lpthread -lm -ldl "
               else
                  LIBS+="-L\${MKLROOT}/lib/intel64 -Wl,--no-as-needed -lmkl_gf_lp64 -lmkl_gnu_thread -lmkl_core -lgomp -lpthread -lm -ldl "
               fi
            fi
         fi
      else
         dnl sequential
         if test "x$evsl_using_intel_mkl_static" = "xyes"
         then
            dnl static link
            LIBS+="-Wl,--start-group \${MKLROOT}/lib/intel64/libmkl_intel_lp64.a \${MKLROOT}/lib/intel64/libmkl_sequential.a \${MKLROOT}/lib/intel64/libmkl_core.a -Wl,--end-group -lpthread -lm -ldl "
         else
            dnl dynamic link
            if test "x$LINK_CC" = "xicc"
            then
               LIBS+="-L\${MKLROOT}/lib/intel64 -lmkl_intel_lp64 -lmkl_sequential -lmkl_core -lpthread -lm -ldl "
            else
               LIBS+="-L\${MKLROOT}/lib/intel64 -Wl,--no-as-needed -lmkl_intel_lp64 -lmkl_sequential -lmkl_core -lpthread -lm -ldl "
            fi
         fi
      fi dnl openmp
   else
      if test "x$HOSTARCH" = "xDarwin"
      then
      dnl
      LIBS+=" TODO "
      fi
   fi
   EVSL_WITH_MKL="YES"
else
   EVSL_WITH_MKL="NO"
fi

dnl *********************************************************************
dnl * CUDA
dnl *********************************************************************
AS_IF([test "x$evsl_using_cuda" = "xyes"],
      [AC_CHECK_HEADERS(["${CUDA_HOME}/include/cuda.h"], [evsl_found_cuda=yes], [evsl_found_cuda=no])],
      [])

if test "$evsl_using_cuda" = "yes"
then
   AC_SUBST(CUDA_HOME)
   if test "x$evsl_found_cuda" != "xyes"
   then
      AC_MSG_WARN([****************************************************])
      AC_MSG_WARN([unable to find cuda.h. Ensure that CUDA_HOME is set])
      AC_MSG_WARN([****************************************************])
   fi
   AC_DEFINE(EVSL_USING_CUDA_GPU, 1, [CUDA being used])
   AC_DEFINE(EVSL_USING_DEVICE_MEMORY, 1, [EVSL WITH DEVICE MEMORY])
   dnl let CC/CXX and LINK be CUCC and let host compiler be CC
   AC_CHECK_PROGS(CUCC, nvcc)
   NVCCBIN=${CC}
   CUCC="${CUCC} -ccbin=$NVCCBIN"
   LINK_CC="${CUCC}"
   LINK_FC="${CUCC}"
   dnl CUDA SM
   if test "x$EVSL_CUDA_SM" = "x"
   then
      EVSL_CUDA_SM=60
   fi
   EVSL_CUDA_GENCODE="-gencode arch=compute_${EVSL_CUDA_SM},\"code=sm_${EVSL_CUDA_SM}\""
   if test "x$evsl_with_debug" = "xyes"
   then
      CUFLAGS+="-g -O0 ${EVSL_CUDA_GENCODE} -dc --x cu"
   else
      CUFLAGS+="-O2 ${EVSL_CUDA_GENCODE} -dc --x cu"
   fi
   CUFLAGS="${CUFLAGS} -Xcompiler \"${CFLAGS}\""
   LDFLAGS="${EVSL_CUDA_GENCODE} -Xcompiler \"${LDFLAGS}\""
   LIBS+="-L\${CUDA_HOME}/lib64 -lcudart -lcublas -lcusparse -lcurand "
   EVSL_WITH_CUDA="YES"
else
   EVSL_WITH_CUDA="NO"
fi

dnl *********************************************************************
dnl * Shared libraries
dnl *********************************************************************
if test "x$evsl_using_shared_lib" = "xyes"
then
   case $HOSTARCH in
      DARWIN* | darwin* | Darwin*) SHARED_BUILD_FLAG="-dynamiclib -undefined dynamic_lookup"
                                   EVSL_LIBSUFFIX=".dylib"
                                   SHARED_SET_SONAME="-install_name @rpath/"
                                   SHARED_OPTIONS="-undefined error" ;;
      LINUX* | linux* | Linux*)    SHARED_BUILD_FLAG="-shared"
                                   EVSL_LIBSUFFIX=".so"
                                   SHARED_SET_SONAME="-Wl,-soname,"
                                   SHARED_OPTIONS="-Wl,-z,defs"
                                   SHARED_COMPILE_FLAG="-fPIC" ;;
   esac
   if test "$evsl_using_cuda" = "yes"
   then
      SHARED_BUILD_FLAG="${SHARED_BUILD_FLAG} ${EVSL_CUDA_GENCODE}"
   fi
   SHARED_BUILD_FLAG="${SHARED_BUILD_FLAG} ${EXTRA_BUILD_FLAGS}"
   FFLAGS="${FFLAGS} ${SHARED_COMPILE_FLAG}"
   BUILD_FC_SHARED="${FC} ${SHARED_BUILD_FLAG}"
   CFLAGS="${CFLAGS} ${SHARED_COMPILE_FLAG} "
   BUILD_CC_SHARED="${CC} ${SHARED_BUILD_FLAG}"
else
   EVSL_LIBSUFFIX=".a"
fi

if test "x$evsl_using_evsl_blas" = "xyes"
then
   AC_DEFINE(EVSL_USING_EVSL_BLAS, 1, [Using internal BLAS routines in EVSL])
fi

if test "x$evsl_using_evsl_lapack" = "xyes"
then
   AC_DEFINE(EVSL_USING_EVSL_LAPACK, 1, [Using internal LAPACK routines in EVSL])
fi


dnl *********************************************************************
dnl * direct solver option
dnl *********************************************************************
if test "x$evsl_using_evsl_cxsparse" = "xyes"
then
   AC_MSG_NOTICE([*****************************************************************])
   AC_MSG_NOTICE([* The CXSparse distributed with EVSL is chosen as direct solver *])
   AC_MSG_NOTICE([* CHOLMOD and UMFPACK in SuiteSparse,  and Pardiso in Intel MKL *])
   AC_MSG_NOTICE([* are also supported,  which can often yield higher performance *])
   AC_MSG_NOTICE([*****************************************************************])
   DIRECTSOL="CXSPARSE"
   DIRECTSOL_INTERFACE="evsl_cxsparse.o"
   LIBDIRECTSOL="-L$EVSL_SRCDIR/DIRECT/CXSparse/Lib -lcxsparse"
fi

if test "x$evsl_using_suitesparse" = "xyes"
then
   if test "x$evsl_using_evsl_blas" = "xyes"
   then
      AC_MSG_WARN([****************************************])
      AC_MSG_WARN([* BLAS is not provided for Suitesparse *])
      AC_MSG_WARN([****************************************])
   fi
   if test "x$evsl_using_evsl_lapack" = "xyes"
   then
      AC_MSG_WARN([******************************************])
      AC_MSG_WARN([* LAPACK is not provided for Suitesparse *])
      AC_MSG_WARN([******************************************])
   fi

   if test "x$evsl_with_metis" = "xno"
   then
      AC_MSG_WARN([****************************************])
      AC_MSG_WARN([* Suitesparse may require METIS (5.1)  *])
      AC_MSG_WARN([* directory of METIS is not provided   *])
      AC_MSG_WARN([* Set it by --with-metis-dir=DIR       *])
      AC_MSG_WARN([****************************************])
      if test "x$HOSTARCH" = "xLinux"
      then
         METIS_DIR="$SUITESPARSE_DIR/metis-5.1.0/build/Linux-x86_64"
      else
         METIS_DIR="$SUITESPARSE_DIR/metis-5.1.0/build/Darwin-x86_64"
      fi
   fi
   DIRECTSOL="SUITESPARSE"
   DIRECTSOL_INTERFACE="evsl_suitesparse.o"
   LIBDIRECTSOL="-L\${SUITESPARSE_DIR}/UMFPACK/Lib -lumfpack                  \\
                       -L\${SUITESPARSE_DIR}/SuiteSparse_config -lsuitesparseconfig \\
                       -L\${SUITESPARSE_DIR}/CHOLMOD/Lib -lcholmod                  \\
                       -L\${SUITESPARSE_DIR}/AMD/Lib/ -lamd                         \\
                       -L\${SUITESPARSE_DIR}/COLAMD/Lib -lcolamd                    \\
                       -L\${METIS_DIR}/libmetis -lmetis                             \\
                       -L\${SUITESPARSE_DIR}/CCOLAMD/Lib -lccolamd                  \\
                       -L\${SUITESPARSE_DIR}/CAMD/Lib -lcamd"
fi

if test "x$evsl_cholmod_use_long" = "xyes"
then
   AC_DEFINE(EVSL_CHOLMOD_USE_LONG, 1, [Using long int version of Cholmod in SuiteSparse interface])
fi

if test "x$evsl_using_mkl_pardiso" = "xyes"
then
   DIRECTSOL="MKL_PARDISO"
   DIRECTSOL_INTERFACE="evsl_pardiso.o"
fi


dnl *********************************************************************
dnl * basic data types
dnl *********************************************************************
if test "x$evsl_using_int" = "xyes"
then
  AC_DEFINE(EVSL_Int, [int], [EVSL data type for integer])
  AC_DEFINE(EVSL_Unsigned, [unsigned], [EVSL data type for unsigned integer])
  AC_DEFINE(EVSL_LongInt, [long int], [EVSL data type for long integer])
fi

if test "x$evsl_using_double" = "xyes"
then
  AC_DEFINE(EVSL_Real, [double], [EVSL data type for real])
  AC_DEFINE(EVSL_Complex, [double _Complex], [EVSL data type for complex])
fi

dnl *********************************************************************
dnl * AC_SUBST performs the variable substitutions so they may be used in
dnl * the makefile.in files. Some macros call AC_SUBST for some of these
dnl * variables, but it does no damage to call it again.
dnl *********************************************************************

dnl *********************************************************************
dnl * compilers and load flags
dnl *********************************************************************
AC_SUBST(CC)
AC_SUBST(CUCC)
AC_SUBST(FC)
AC_SUBST(CFLAGS)
AC_SUBST(CUFLAGS)
AC_SUBST(FCFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(EVSL_WITH_CUDA)

AC_SUBST(LINK_FC)
AC_SUBST(LINK_CC)

AC_SUBST(BUILD_FC_SHARED)
AC_SUBST(BUILD_CC_SHARED)
AC_SUBST(SHARED_SET_SONAME)
AC_SUBST(SHARED_OPTIONS)

AC_SUBST(LIBBLAS)
AC_SUBST(LIBBLAS_DIR)
AC_SUBST(LIBLAPACK)
AC_SUBST(LIBLAPACK_DIR)

dnl *********************************************************************
dnl * INTEL MKL related information
dnl *********************************************************************
AC_SUBST(EVSL_WITH_MKL)

dnl *********************************************************************
dnl * Direct solver
dnl *********************************************************************
AC_SUBST(DIRECTSOL)
AC_SUBST(DIRECTSOL_INTERFACE)
AC_SUBST(METIS_DIR)
AC_SUBST(SUITESPARSE_DIR)
AC_SUBST(LIBDIRECTSOL)

dnl *********************************************************************
dnl * ar & ranlib substitution
dnl *********************************************************************
AC_SUBST(AR)
AC_SUBST(RANLIB)

dnl *********************************************************************
dnl * Installation Directories
dnl *********************************************************************
dnl AC_SUBST(EVSL_INSTALLDIR)
dnl AC_SUBST(EVSL_LIBINSTALL)
dnl AC_SUBST(EVSL_INCINSTALL)

dnl *********************************************************************
dnl * General purpose header files, libraries and directories
dnl *********************************************************************
AC_SUBST(LIBS_DIR)
AC_SUBST(LIBS)
AC_SUBST(FLIBS)
AC_SUBST(FCLIBS)
dnl *********************************************************************
dnl * suffix for evsl libraries
dnl *********************************************************************
AC_SUBST(EVSL_LIBSUFFIX)

dnl *********************************************************************
dnl * Define the files to be configured and made 
dnl *********************************************************************
AC_CONFIG_FILES([makefile.in:CONFIG/makefile.in.config])

AC_OUTPUT
